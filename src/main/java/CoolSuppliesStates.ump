namespace ca.mcgill.ecse.coolsupplies.model;

class Order {
  depend ca.mcgill.ecse.coolsupplies.model.BundleItem.PurchaseLevel;
  
  status {
    Started {
      updateOrder(PurchaseLevel level, Student student) [isChildOfParent(student)] / {
        setLevel(level);
        setStudent(student);
      }
      -> Started;
      updateOrder(PurchaseLevel level, Student student) [!isChildOfParent(student)] / {
        rejectUpdateOrder(level, student);
      }
      -> Started;
      addItem(InventoryItem item, int quantity) [!inventoryItemIsInOrder(item) && quantity > 0] / {
        addOrderItem(quantity, coolSupplies, item);
      }
      -> Started;
      addItem(InventoryItem item, int quantity) [quantity <= 0] / {
        rejectAddItem(item, quantity);
      }
      -> Started;
      addItem(InventoryItem item, int quantity) [inventoryItemIsInOrder(item)] / {
        rejectAddItem(item, quantity);
      }
      -> Started;
      updateItem(OrderItem item, int quantity) [quantity > 0] / {
        item.setQuantity(quantity);
      }
      -> Started;
      updateItem(OrderItem item, int quantity) [quantity <= 0] / {
        rejectUpdateItem(item, quantity);
      }
      -> Started;
      deleteItem(OrderItem item) [inventoryItemIsInOrder(item.getItem())] / {
        item.delete();
      }
      -> Started;
      deleteItem(OrderItem item) [!inventoryItemIsInOrder(item.getItem())] / {
        rejectDeleteItem(item);
      }
      -> Started;
      pay(String authorizationCode) [codeIsValid(authorizationCode)] / {
        setAuthorizationCode(authorizationCode);
      }
      -> Paid;
      pay(String authorizationCode) [!codeIsValid(authorizationCode)] / {
        rejectPay(authorizationCode);
      }
      -> Started;
      startSchoolYear() -> Penalized;
      cancelOrder() -> Final;
    }
    Paid {
      cancelOrder() -> Final;
      startSchoolYear() -> Prepared;
    }
    Penalized {
      payPenalty(String authorizationCode, String penaltyAuthCode) [codeIsValid(authorizationCode) && codeIsValid(penaltyAuthCode)] / {
        setAuthorizationCode(authorizationCode);
        setPenaltyAuthorizationCode(penaltyAuthCode);
      }
      -> Prepared;
      payPenalty(String authorizationCode, String penaltyAuthCode) [!codeIsValid(authorizationCode)] / {
        rejectPayPenalty(authorizationCode, penaltyAuthCode);
      }
      -> Penalized;
      payPenalty(String authorizationCode, String penaltyAuthCode) [!codeIsValid(penaltyAuthCode)] / {
        rejectPayPenalty(authorizationCode, penaltyAuthCode);
      }
      -> Penalized;
    }
    Prepared {
      pickUp() -> PickedUp;
    }
    PickedUp {
    }
  }

  private Boolean isChildOfParent(Student student) {
    return student.getParent() == getParent();
  }

  private Boolean inventoryItemIsInOrder(InventoryItem item) {
    for (OrderItem orderItem : getOrderItems()) {
      if (orderItem.getItem() == item) {
        return true;
      }
    }
    return false;
  }

  private Boolean codeIsValid(String code) {
    return code != null && !code.contains(" ") && !code.isEmpty();
  }

  private void rejectUpdateOrder(PurchaseLevel level, Student student) {
    throw new RuntimeException("Student" + student.getName() + " is not a child of the parent " + getParent().getEmail() + ".");
  }

  private void rejectAddItem(InventoryItem item, int quantity) {
    if (quantity <=0) {
      throw new RuntimeException("Quantity must be greater than 0.");
    }
    if (inventoryItemIsInOrder(item)) {
      throw new RuntimeException("Item " + item.getName() + " already exists in the order " + getNumber() + ".");
    }
  }

  private void rejectUpdateItem(OrderItem item, int quantity) {
    throw new RuntimeException("Quantity must be greater than 0.");
  }

  private void rejectDeleteItem(OrderItem item) {
    throw new RuntimeException("Item " + item.getItem().getName() + " does not exist in the order " + getNumber() + ".");
  }

  private void rejectPay (String authorizationCode) {
    throw new RuntimeException("Authorization code is invalid");
  }

  private void rejectPayPenalty (String authorizationCode, String penaltyAuthCode) {
    if (!codeIsValid(authorizationCode)) {
      throw new RuntimeException("Authorization code is invalid");
    }
    if (!codeIsValid(penaltyAuthCode)) {
      throw new RuntimeException("Penalty authorization code is invalid");
    }
  }
}
